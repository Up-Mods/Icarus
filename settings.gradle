pluginManagement {
    repositories {
        gradlePluginPortal()
        maven {
            name = 'Sponge'
            url = 'https://repo.spongepowered.org/repository/maven-public'
        }
        // Currently needed for Intermediary and other temporary dependencies
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net'
        }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.9.0'
}

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            from(files("./Common/libs.versions.toml"))
        }
        forge {
            from(files("./Forge/libs.versions.toml"))
        }
        fabric {
            from(files("./Fabric/libs.versions.toml"))
        }
    }
}

def ENV = System.getenv()

buildCache {
    remote(HttpBuildCache) {
        url = "https://ci-cache.uuid.gg/cache"
        if (ENV.CI && ENV.GRADLE_BUILD_CACHE_USER && ENV.GRADLE_BUILD_CACHE_TOKEN) {
            push = true
            credentials {
                username = ENV.GRADLE_BUILD_CACHE_USER
                password = ENV.GRADLE_BUILD_CACHE_TOKEN
            }
        }
    }
}

rootProject.name = 'Icarus'

include 'Common', 'Fabric', 'Forge'
